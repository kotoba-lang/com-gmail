(ns gmail.watch-test
  (:require [clojure.test :refer [deftest is]]
            [gmail.client :as client]
            [gmail.watch :as watch]))

(deftest watch-posts-topic-name-to-the-watch-endpoint
  (let [captured (atom nil)
        http-fn (fn [req] (reset! captured req)
                  {:status 200 :body "{\"historyId\":\"9\",\"expiration\":\"1700000000000\"}"})
        resp (watch/watch! "projects/my-proj/topics/gmail-push"
                           {:http-fn http-fn :token "t"})]
    (is (= (str client/api-base "/watch") (:url @captured)))
    (is (= :post (:method @captured)))
    ;; data.json escapes `/` as `\/` by default -- Gmail parses either.
    (is (= "{\"topicName\":\"projects\\/my-proj\\/topics\\/gmail-push\"}" (:body @captured)))
    (is (= {:historyId "9" :expiration "1700000000000"} resp))))

(deftest watch-includes-label-ids-when-given
  (let [captured (atom nil)
        http-fn (fn [req] (reset! captured req) {:status 200 :body "{\"historyId\":\"9\"}"})]
    (watch/watch! "projects/p/topics/t"
                  {:http-fn http-fn :token "t" :label-ids ["INBOX" "IMPORTANT"]})
    (is (= "{\"topicName\":\"projects\\/p\\/topics\\/t\",\"labelIds\":[\"INBOX\",\"IMPORTANT\"]}"
           (:body @captured)))))

(deftest watch-omits-label-ids-when-not-given
  (let [captured (atom nil)
        http-fn (fn [req] (reset! captured req) {:status 200 :body "{\"historyId\":\"9\"}"})]
    (watch/watch! "projects/p/topics/t" {:http-fn http-fn :token "t"})
    ;; no labelIds key at all -- whole-mailbox watch
    (is (not (re-find #"labelIds" (:body @captured))))))

(deftest stop-posts-to-the-stop-endpoint-with-no-body
  (let [captured (atom nil)
        http-fn (fn [req] (reset! captured req) {:status 200 :body ""})]
    (is (nil? (watch/stop! {:http-fn http-fn :token "t"})))
    (is (= (str client/api-base "/stop") (:url @captured)))
    (is (= :post (:method @captured)))
    (is (nil? (:body @captured)))))

(deftest watch-throws-on-non-2xx
  (let [http-fn (fn [_req] {:status 403 :body "{\"error\":\"no publish rights\"}"})]
    (is (thrown-with-msg?
         #?(:clj clojure.lang.ExceptionInfo :cljs js/Error)
         #"Gmail API request failed"
         (watch/watch! "projects/p/topics/t" {:http-fn http-fn :token "t"})))))
